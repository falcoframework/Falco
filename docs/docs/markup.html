<!DOCTYPE html><html lang="en"><head><meta charset="UTF-8" /><meta http-equiv="X-UA-Compatible" content="IE=edge, chrome=1" /><meta name="viewport" content="width=device-width, initial-scale=1" /><title>Falco - F# web toolkit for ASP.NET Core</title><meta name="description" content="A functional-first toolkit for building brilliant ASP.NET Core applications using F#." /><link rel="shortcut icon" href="/favicon.ico" type="image/x-icon" /><link rel="icon" href="/favicon.ico" type="image/x-icon" /><link rel="preconnect" href="https://fonts.gstatic.com" /><link href="https://fonts.googleapis.com/css2?family=Noto+Sans+JP:wght@400;700&display=swap" rel="stylesheet" /><link href="/prism.css" rel="stylesheet" /><link href="/tachyons.css" rel="stylesheet" /><link href="/style.css" rel="stylesheet" /><script async src="https://www.googletagmanager.com/gtag/js?id=G-D62HSJHMNZ"></script><script>window.dataLayer = window.dataLayer || [];
                function gtag(){dataLayer.push(arguments);}
                gtag('js', new Date());
                gtag('config', 'G-D62HSJHMNZ');</script></head><body class="noto lh-copy"><div class="min-vh-100 mw9 center pa3 overflow-hidden"><nav class="sidebar w-20-l fl-l mb3 mb0-l"><div class="flex items-center"><a href="/docs" class="db w3 w4-l"><img src="/brand.svg" class="br3" /></a><h2 class="dn-l mt3 ml3 fw4 gray">Falco Documentation</h2></div><div class="dn db-l"><h3>Contents</h3><ul class="nl3 f6"><li><a href="get-started.html">Getting Started</a></li><li><a href="routing.html">Routing</a></li><li><a href="response.html">Writing responses</a></li><li><a href="request.html">Accessing request data</a></li><li><a href="markup.html">View engine</a></li><li><a href="cross-site-request-forgery.html">Security</a><ul><li><a href="cross-site-request-forgery.html">Cross Site Request Forgery (XSRF)</a></li><li><a href="authentication.html">Authentication & Authorization</a></li></ul></li><li><a href="example-hello-world.html">Examples</a><ul><li><a href="example-hello-world.html">Hello World</a></li><li><a href="example-hello-world-mvc.html">Hello World MVC</a></li><li><a href="example-dependency-injection.html">Dependency Injection</a></li><li><a href="example-external-view-engine.html">Hello World</a></li><li><a href="example-basic-rest-api.html">Basic REST API</a></li></ul></li><li><a href="migrating-from-v4-to-v5.html">V5 Migration Guide</a></li></ul></div></nav><main class="w-80-l fl-l pl3-l"><h1 id="markup">Markup</h1>
<p>Falco.Markup is broken down into three primary modules, <code>Elem</code>, <code>Attr</code> and <code>Text</code>, which are used to generate elements, attributes and text nodes respectively. Each module contain a suite of functions mapping to the various element/attribute/node names. But can also be extended to create custom elements and attributes.</p>
<p>Primary elements are broken down into two types, <code>ParentNode</code> or <code>SelfClosingNode</code>.</p>
<p><code>ParentNode</code> elements are those that can contain other elements. Represented as functions that receive two inputs: attributes and optionally elements.</p>
<pre><code class="language-fsharp">let markup =
    Elem.div [ Attr.class' &quot;heading&quot; ] [
        Text.h1 &quot;Hello world!&quot; ]
</code></pre>
<p><code>SelfClosingNode</code> elements are self-closing tags. Represented as functions that receive one input: attributes.</p>
<pre><code class="language-fsharp">let markup =
    Elem.div [ Attr.class' &quot;divider&quot; ] [
        Elem.hr [] ]
</code></pre>
<p>Text is represented using the <code>TextNode</code> and created using one of the functions in the <code>Text</code> module.</p>
<pre><code class="language-fsharp">let markup =
    Elem.div [] [
        Text.comment &quot;An HTML comment&quot;
        Text.p &quot;A paragraph&quot;
        Elem.p [] [ Text.rawf &quot;Hello %s&quot; &quot;Jim&quot; ]
        Elem.code [] [ Text.enc &quot;&lt;div&gt;Hello&lt;/div&gt;&quot; ] // HTML encodes text before rendering
    ]
</code></pre>
<p>Attributes contain two subtypes as well, <code>KeyValueAttr</code> which represent key/value attributes or <code>NonValueAttr</code> which represent boolean attributes.</p>
<pre><code class="language-fsharp">let markup =
    Elem.input [ Attr.type' &quot;text&quot;; Attr.required ]
</code></pre>
<p>Most <a href="https://developer.mozilla.org/en-US/docs/Web/Events">JavaScript Events</a> have also been mapped in the <code>Attr</code> module. All of these events are prefixed with the word &quot;on&quot; (i.e., <code>Attr.onclick</code>, <code>Attr.onfocus</code> etc.)</p>
<pre><code class="language-fsharp">let markup =
    Elem.button [ Attr.onclick &quot;console.log(\&quot;hello world\&quot;)&quot; ] [ Text.raw &quot;Click me&quot; ]
</code></pre>
<h2 id="html">HTML</h2>
<p>Though Falco.Markup can be used to produce any markup. It is first and foremost an HTML library.</p>
<h3 id="combining-views-to-create-complex-output">Combining views to create complex output</h3>
<pre><code class="language-fsharp">open Falco.Markup

// Components
let divider =
    Elem.hr [ Attr.class' &quot;divider&quot; ]

// Template
let master (title : string) (content : XmlNode list) =
    Elem.html [ Attr.lang &quot;en&quot; ] [
        Elem.head [] [
            Elem.title [] [ Text.raw title ]
        ]
        Elem.body [] content
    ]

// Views
let homeView =
    master &quot;Homepage&quot; [
        Text.h1 &quot;Homepage&quot;
        divider
        Text.p &quot;Lorem ipsum dolor sit amet, consectetur adipiscing.&quot;
    ]

let aboutView =
    master &quot;About Us&quot; [
        Text.h1 &quot;About&quot;
        divider
        Text.p &quot;Lorem ipsum dolor sit amet, consectetur adipiscing.&quot;
    ]
</code></pre>
<h3 id="strongly-typed-views">Strongly-typed views</h3>
<pre><code class="language-fsharp">open Falco.Markup

type Person =
    { FirstName : string
      LastName : string }

let doc (person : Person) =
    Elem.html [ Attr.lang &quot;en&quot; ] [
        Elem.head [] [
            Elem.title [] [ Text.raw &quot;Sample App&quot; ]
        ]
        Elem.body [] [
            Elem.main [] [
                Text.h1 &quot;Sample App&quot;
                Text.p $&quot;{person.First} {person.Last}&quot;
            ]
        ]
    ]
</code></pre>
<h3 id="forms">Forms</h3>
<p>Forms are the lifeblood of HTML applications. A basic form using the markup module would like the following:</p>
<pre><code class="language-fsharp">let dt = DateTime.Now

Elem.form [ Attr.methodPost; Attr.action &quot;/submit&quot; ] [
    Elem.label [ Attr.for' &quot;name&quot; ] [ Text.raw &quot;Name&quot; ]
    Elem.input [ Attr.id &quot;name&quot;; Attr.name &quot;name&quot;; Attr.typeText ]

    Elem.label [ Attr.for' &quot;birthdate&quot; ] [ Text.raw &quot;Birthday&quot; ]
    Elem.input [ Attr.id &quot;birthdate&quot;; Attr.name &quot;birthdate&quot;; Attr.typeDate; Attr.valueDate dt ]

    Elem.input [ Attr.typeSubmit ]
]
</code></pre>
<p>Expanding on this, we can create a more complex form involving multiple inputs and input types as follows:</p>
<pre><code class="language-fsharp">Elem.form [ Attr.method &quot;post&quot;; Attr.action &quot;/submit&quot; ] [
    Elem.label [ Attr.for' &quot;name&quot; ] [ Text.raw &quot;Name&quot; ]
    Elem.input [ Attr.id &quot;name&quot;; Attr.name &quot;name&quot; ]

    Elem.label [ Attr.for' &quot;bio&quot; ] [ Text.raw &quot;Bio&quot; ]
    Elem.textarea [ Attr.name &quot;id&quot;; Attr.name &quot;bio&quot; ] []

    Elem.label [ Attr.for' &quot;hobbies&quot; ] [ Text.raw &quot;Hobbies&quot; ]
    Elem.select [ Attr.id &quot;hobbies&quot;; Attr.name &quot;hobbies&quot;; Attr.multiple ] [
        Elem.option [ Attr.value &quot;programming&quot; ] [ Text.raw &quot;Programming&quot; ]
        Elem.option [ Attr.value &quot;diy&quot; ] [ Text.raw &quot;DIY&quot; ]
        Elem.option [ Attr.value &quot;basketball&quot; ] [ Text.raw &quot;Basketball&quot; ]
    ]

    Elem.fieldset [] [
        Elem.legend [] [ Text.raw &quot;Do you like chocolate?&quot; ]
        Elem.label [] [
            Text.raw &quot;Yes&quot;
            Elem.input [ Attr.typeRadio; Attr.name &quot;chocolate&quot;; Attr.value &quot;yes&quot; ] ]
        Elem.label [] [
            Text.raw &quot;No&quot;
            Elem.input [ Attr.typeRadio; Attr.name &quot;chocolate&quot;; Attr.value &quot;no&quot; ] ]
    ]

    Elem.fieldset [] [
        Elem.legend [] [ Text.raw &quot;Subscribe to our newsletter&quot; ]
        Elem.label [] [
            Text.raw &quot;Receive updates about product&quot;
            Elem.input [ Attr.typeCheckbox; Attr.name &quot;newsletter&quot;; Attr.value &quot;product&quot; ] ]
        Elem.label [] [
            Text.raw &quot;Receive updates about company&quot;
            Elem.input [ Attr.typeCheckbox; Attr.name &quot;newsletter&quot;; Attr.value &quot;company&quot; ] ]
    ]

    Elem.input [ Attr.typeSubmit ]
]
</code></pre>
<p>A simple but useful <em>meta</em>-element <code>Elem.control</code> can reduce the verbosity required to create form outputs. The same form would look like:</p>
<pre><code class="language-fsharp">Elem.form [ Attr.method &quot;post&quot;; Attr.action &quot;/submit&quot; ] [
    Elem.control &quot;name&quot; [] [ Text.raw &quot;Name&quot; ]

    Elem.controlTextarea &quot;bio&quot; [] [ Text.raw &quot;Bio&quot; ] []

    Elem.controlSelect &quot;hobbies&quot; [ Attr.multiple ] [ Text.raw &quot;Hobbies&quot; ] [
        Elem.option [ Attr.value &quot;programming&quot; ] [ Text.raw &quot;Programming&quot; ]
        Elem.option [ Attr.value &quot;diy&quot; ] [ Text.raw &quot;DIY&quot; ]
        Elem.option [ Attr.value &quot;basketball&quot; ] [ Text.raw &quot;Basketball&quot; ]
    ]

    Elem.fieldset [] [
        Elem.legend [] [ Text.raw &quot;Do you like chocolate?&quot; ]
        Elem.control &quot;chocolate&quot; [ Attr.id &quot;chocolate_yes&quot;; Attr.typeRadio ] [ Text.raw &quot;yes&quot; ]
        Elem.control &quot;chocolate&quot; [ Attr.id &quot;chocolate_no&quot;; Attr.typeRadio ] [ Text.raw &quot;no&quot; ]
    ]

    Elem.fieldset [] [
        Elem.legend [] [ Text.raw &quot;Subscribe to our newsletter&quot; ]
        Elem.control &quot;newsletter&quot; [ Attr.id &quot;newsletter_product&quot;; Attr.typeCheckbox ] [ Text.raw &quot;Receive updates about product&quot; ]
        Elem.control &quot;newsletter&quot; [ Attr.id &quot;newsletter_company&quot;; Attr.typeCheckbox ] [ Text.raw &quot;Receive updates about company&quot; ]
    ]

    Elem.input [ Attr.typeSubmit ]
]
</code></pre>
<h3 id="attribute-value">Attribute Value</h3>
<p>One of the more common places of sytanctic complexity is with <code>Attr.value</code> which expects, like all <code>Attr</code> functions, <code>string</code> input. Some helpers exist to simplify this.</p>
<pre><code class="language-fsharp">let dt = DateTime.Now

Elem.input [ Attr.typeDate; Attr.valueStringf &quot;yyyy-MM-dd&quot; dt ]

// you could also just use:
Elem.input [ Attr.typeDate; Attr.valueDate dt ] // formatted to ISO-8601 yyyy-MM-dd

// or,
Elem.input [ Attr.typeMonth; Attr.valueMonth dt ] // formatted to ISO-8601 yyyy-MM

// or,
Elem.input [ Attr.typeWeek; Attr.valueWeek dt ] // formatted to Gregorian yyyy-W#

// it works for TimeSpan too:
let ts = TimeSpan(12,12,0)
Elem.input [ Attr.typeTime; Attr.valueTime ts ] // formatted to hh:mm

// there is a helper for Option too:
let someTs = Some ts
Elem.input [ Attr.typeTime; Attr.valueOption Attr.valueTime someTs ]
</code></pre>
<h3 id="merging-attributes">Merging Attributes</h3>
<p>The markup module allows you to easily create components, an excellent way to reduce code repetition in your UI. To support runtime customization, it is advisable to ensure components (or reusable markup blocks) retain a similar function &quot;shape&quot; to standard elements. That being, <code>XmlAttribute list -&gt; XmlNode list -&gt; XmlNode</code>.</p>
<p>This means that you will inevitably end up needing to combine your predefined <code>XmlAttribute list</code> with a list provided at runtime. To facilitate this, the <code>Attr.merge</code> function will group attributes by key, and intelligently concatenate the values in the case of additive attributes (i.e., <code>class</code>, <code>style</code> and <code>accept</code>).</p>
<pre><code class="language-fsharp">open Falco.Markup

// Components
let heading (attrs : XmlAttribute list) (content : XmlNode list) =
    // safely combine the default XmlAttribute list with those provided
    // at runtime
    let attrs' =
        Attr.merge [ Attr.class' &quot;text-large&quot; ] attrs

    Elem.div [] [
        Elem.h1 [ attrs' ] content
    ]

// Template
let master (title : string) (content : XmlNode list) =
    Elem.html [ Attr.lang &quot;en&quot; ] [
        Elem.head [] [
            Elem.title [] [ Text.raw title ]
        ]
        Elem.body [] content
    ]

// Views
let homepage =
    master &quot;Homepage&quot; [
        heading [ Attr.class' &quot;red&quot; ] [ Text.raw &quot;Welcome to the homepage&quot; ]
        Text.p &quot;Lorem ipsum dolor sit amet, consectetur adipiscing.&quot;
    ]

let homepage =
    master &quot;About Us&quot; [
        heading [ Attr.class' &quot;purple&quot; ] [ Text.raw &quot;This is what we're all about&quot; ]
        Text.p &quot;Lorem ipsum dolor sit amet, consectetur adipiscing.&quot;
    ]
</code></pre>
<h2 id="custom-elements-attributes">Custom Elements &amp; Attributes</h2>
<p>Every effort has been taken to ensure the HTML and SVG specs are mapped to functions in the module. In the event an element or attribute you need is missing, you can either file an <a href="https://github.com/pimbrouwers/Falco.Markup/issues">issue</a>, or more simply extend the module in your project.</p>
<p>An example creating custom XML elements and using them to create a structured XML document:</p>
<pre><code class="language-fsharp">open Falco.Makrup

module Elem =
    let books = Elem.create &quot;books&quot;
    let book = Elem.create &quot;book&quot;
    let name = Elem.create &quot;name&quot;

module Attr =
    let soldOut = Attr.createBool &quot;soldOut&quot;

let xmlDoc =
    Elem.books [] [
        Elem.book [ Attr.soldOut ] [
            Elem.name [] [ Text.raw &quot;To Kill A Mockingbird&quot; ]
        ]
    ]

let xml = renderXml xmlDoc
</code></pre>
<h2 id="svg">SVG</h2>
<p>Much of the SVG spec has been mapped to element and attributes functions. There is also an SVG template to help initialize a new drawing with a valid viewbox.</p>
<pre><code class="language-fsharp">open Falco.Markup
open Falco.Markup.Svg

// https://developer.mozilla.org/en-US/docs/Web/SVG/Element/text#example
let svgDrawing =
    Templates.svg (0, 0, 240, 80) [
        Elem.style [] [
            Text.raw &quot;.small { font: italic 13px sans-serif; }&quot;
            Text.raw &quot;.heavy { font: bold 30px sans-serif; }&quot;
            Text.raw &quot;.Rrrrr { font: italic 40px serif; fill: red; }&quot;
        ]
        Elem.text [ Attr.x &quot;20&quot;; Attr.y &quot;35&quot;; Attr.class' &quot;small&quot; ] [ Text.raw &quot;My&quot; ]
        Elem.text [ Attr.x &quot;40&quot;; Attr.y &quot;35&quot;; Attr.class' &quot;heavy&quot; ] [ Text.raw &quot;cat&quot; ]
        Elem.text [ Attr.x &quot;55&quot;; Attr.y &quot;55&quot;; Attr.class' &quot;small&quot; ] [ Text.raw &quot;is&quot; ]
        Elem.text [ Attr.x &quot;65&quot;; Attr.y &quot;55&quot;; Attr.class' &quot;Rrrrr&quot; ] [ Text.raw &quot;Grumpy!&quot; ]
    ]

let svg = renderNode svgDrawing
</code></pre>
<p><a href="cross-site-request-forgery.html">Next: Cross-site Request Forgery (XSRF)</a></p>
</main></div><footer class="cl pa3 bg-merlot"><div class="f7 tc white-70">&copy; 2020-2025 Pim Brouwers & contributors.</div></footer><script src="/prism.js"></script></body></html>